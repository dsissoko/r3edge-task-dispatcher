plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.r3edge'
version = '0.0.1'

base {
    archivesName = 'r3edge-task-dispatcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('springCloudVersion', "2024.0.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
}


dependencies {
    // Annotations et runtime
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

    // Spring minimum requis pour la lib
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.boot:spring-boot-starter-aop'
    api 'org.springframework.cloud:spring-cloud-starter'

    // ShedLock for distributed locking
    implementation 'net.javacrumbs.shedlock:shedlock-spring:6.9.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc:6.9.0'
    testImplementation 'com.h2database:h2:2.2.224'

    // Pour les tests
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc'
    testImplementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:6.9.0'

}

test {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
    jvmArgs '-Djava.net.preferIPv4Stack=true'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

springBoot {
    buildInfo()
}

tasks.named("bootJar") {
    enabled = false
}

tasks.named("jar") {
    enabled = true
    configure {
        manifest {
            attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'r3edge.com',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Build-Jdk': System.getProperty('java.version')
            )
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = base.archivesName.get()
            version = project.version

            from components.java
            
            versionMapping {
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${project.findProperty('gpr.user') ?: 'DefaultGitHubUser'}/${project.name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("TWEAKED_PAT")
            }
        }
        maven {
            name = "Buildkite"
            url = uri("https://packages.buildkite.com/r3edge/r3edge-libs/maven2/")
            authentication {
                header(HttpHeaderAuthentication)
            }
            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "Bearer ${project.findProperty('BK_TOKEN') ?: System.getenv('BK_TOKEN')}"
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-parameters"
}

tasks.withType(Javadoc).configureEach {
    options.encoding = "UTF-8"
}
